//===== rAthena Script =======================================
//= Euphy's Quest Shop
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.6c
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= A dynamic quest shop based on Lunar's, with easier config.
//= Includes support for multiple shops & cashpoints.
//= Item Preview script by ToastOfDoom.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.2 Added category support.
//= 1.3 More options and fixes.
//= 1.4 Added debug settings.
//= 1.5 Replaced categories with shop IDs.
//= 1.6 Added support for purchasing stackables.
//= 1.6a Added support for previewing costumes and robes.
//= 1.6b Added 'disable_items' command.
//= 1.6c Replaced function 'A_An' with "F_InsertArticle".
//============================================================

// Shop NPCs -- supplying no argument displays entire menu.
//	callfunc "instanceshop"{,<shop ID>{,<shop ID>{,...}}};
//  ADD YOUR NPC HERE
//============================================================
xmas,240,289,3	script	Merchant Master#	4_CAT_MERMASTER,{ callfunc "instanceshop",1;}
1@gef,129,60,4	script	Merchant Master#2	4_CAT_MERMASTER,{ callfunc "instanceshop",2;}
malangdo,139,271,3	script	Merchant Master#3	4_CAT_MERMASTER,{ callfunc "instanceshop",3;}
nyd_dun02,102,192,3	script	Merchant Master#4	4_CAT_MERMASTER,{ callfunc "instanceshop",4;}
moc_para01,27,97,5	script	Merchant Master#5	4_CAT_MERMASTER,{ callfunc "instanceshop",5;}
verus04,167,215,3	script	Merchant Master#6	4_CAT_MERMASTER,{ callfunc "instanceshop",6;}
glast_01,214,292,3	script	Merchant Master#7	4_CAT_MERMASTER,{ callfunc "instanceshop",7;}
rebel_in,76,67,5	script	Merchant Master#8	4_CAT_MERMASTER,{ callfunc "instanceshop",8;}
sp_cor,110,130,5	script	Merchant Master#9	4_CAT_MERMASTER,{ callfunc "instanceshop",9;}
ma_fild01,172,219,5	script	Merchant Master#10	4_CAT_MERMASTER,{ callfunc "instanceshop",10;}
tha_t01,158,77,5	script	Merchant Master#11	4_CAT_MERMASTER,{ callfunc "instanceshop",11;}
lighthalzen,340,291,4	script	Merchant Master#12	4_CAT_MERMASTER,{ callfunc "instanceshop",12;}
//moc_para01,41,165,5	script	Eurynome	76,{ callfunc "instanceshop",7,8,9,10; }	// call the shop 1 and 2 defined below
// etc.. Add your Shop NPCs 'Quest Shop#XXX' here
//============================================================


// Script Core - DO NOT DUPLICATE THIS NPC !!!!!!!!!!!!!
//============================================================
-	script	instance_shop	-1,{
function Add; function Chk; function Slot;
OnInit:
	freeloop(1);

// -----------------------------------------------------------
//  Basic shop settings.
// -----------------------------------------------------------

	set .Announce,1;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .ShowID,1;  	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,1;	// Show Zeny cost, if any? (1: yes / 0: no)
	set .MaxStack,1;	// Max number of quest items purchased at one time.

// -----------------------------------------------------------
//  Points variable -- optional quest requirement.
//	setarray .Points$[0],"<variable name>","<display name>";
// -----------------------------------------------------------

	setarray .Points$[0],
		"#CASHPOINTS", "Cash Points";


//=====================================================================================
// ------------------- ADD YOUR SHOPS NAME AND ITEMS SHOPS STARTING HERE --------------
//=====================================================================================

// -----------------------------------------------------------
//  Shop IDs -- to add shops, copy dummy data at bottom of file.
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
// -----------------------------------------------------------

	setarray .Shops$[1],
		"Bloody Coins",	// Horror Toy
		"Geffen Tournament Coin",	// Geffen magic tournament
		"Sea God's Anger",	// Malangdo
		"Nidhoggur Shadow Garb", // Nidhoggur
		"Tooth of Jitterbug",	// Jitterbug
		"Charleston Scraps",	// Charleston
		"CMagic&CSpell",	// Glastheim
		"Schwartzvald Honor",	// Rebel
		"Cor Cores",	// OS
		"Bangungot Spirits",	// Bangungot
		"Skeletal Armor Piece",	// Thanatos
		"Biolab Drops";	// Biolabs
		

// -----------------------------------------------------------
//  Quest items -- do NOT use a reward item more than once!
//	Add(<shop ID>,<reward ID>,<reward amount>,
//	    <Zeny cost>,<point cost>,
//	    <required item ID>,<required item amount>{,...});
// -----------------------------------------------------------
	
	//Costumes
//	Add(1,20232,1,0,0,7642,5000,670,500); // Costume Celine Ribbon
	Add(1,20590,1,0,0,7642,2000,670,1000); // Evil Druid Cross
//	Add(1,20955,1,0,0,7642,6000,670,750); // Christmas Tree
	Add(1,32237,1,0,0,7642,5000,670,5000); // Celine Brooch
	Add(1,420003,1,0,0,7642,10000,670,5000); // Cd in mouth
	
//	Add(2,31396,1,0,0,6671,5000,670,500); // Sorcerer
//	Add(2,20943,1,0,0,6671,7500,670,750); // Geffen Magic Muffler
//	Add(2,20944,1,0,0,6671,7500,670,750); // Anti Magic Manteau
//	Add(2,18652,1,0,0,6671,10000,670,1000); // Vanargrand
	
//	Add(3,19292,1,0,0,6423,20,670,100); // Cat Knit
	Add(3,31151,1,0,0,6423,15,670,100); // Cat Ears
	Add(3,31028,1,0,0,6423,15,670,100); // Cat Hood
	Add(3,20499,1,0,0,6423,15,670,100); // Cat Hat
	Add(3,31607,1,0,0,6423,15,670,100); // Cat Witch Hat
	Add(3,31186,1,0,0,6423,15,670,100); // Real Black Cat
	Add(3,31533,1,0,0,6423,15,670,100); // Black cat Muffler
	Add(3,31300,1,0,0,6423,15,670,100); // White Cat Muffler
	Add(3,20598,1,0,0,6423,75,670,1000); // Cat Bag Black
	Add(3,20517,1,0,0,6423,75,670,1000); // Cat Bag White
	
	Add(4,20212,1,0,0,2554,2,670,100); // Nidhog & loki
	Add(4,31149,1,0,0,2554,2,670,100); // Nidhog
//	Add(4,31406,1,0,0,2554,10,670,500); // Nidhog w
	
	Add(5,31674,1,0,0,6719,50,670,100); // Jitterbug
	Add(5,400000,1,0,0,6719,500,670,5000); // Whistle
	
	Add(6,20318,1,0,0,6752,300,6961,10,6962,5,670,100); // Charleston
	
	Add(7,20432,1,0,0,6755,25,670,100); // Khalitzburg
	Add(7,31332,1,0,0,6755,25,670,100); // Khalitzburg Black
	Add(7,31377,1,0,0,6608,1200,670,100); // Glast Obs
	
	Add(8,20597,1,0,0,25155,2500,13460,1,13461,1,13462,1,670,1000); // Heart Hunter
	
	Add(9,31463,1,0,0,25723,50,670,100); // Flying Drone
	
	Add(10,19240,1,0,0,2911,2,670,5000); // Syringe
	
	Add(11,20514,1,0,0,7450,100,670,1000); // Thanatos Sword
	
	Add(12,20405,1,0,0,1233,1,6471,100,6469,100,6470,100,670,100); // Eremes Scarf
	Add(12,20132,1,0,0,6471,300,6469,300,6470,300,670,100); // Aura
	Add(12,19244,1,0,0,1528,3,6471,1000,6469,1000,6470,1000,670,5000); // Rosary

// -----------------------------------------------------------

//=====================================================================================
// ------------------- YOUR SHOPS AND ITEMS SHOPS HAVE BEEN ADDED ---------------------
//=====================================================================================

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "instanceshop"+.@i,909;
	}
	end;

OnMenu:

	mes "[^FF6600Merchant Master^000000]";
	mes "Hello";
	mes "I'm the merchant master.";
	mes "I can sell you stuff if you have-";
	mes "something i want.";
	
	set .@size, getarraysize(@i);
	if (!.@size) set @shop_index, select(.menu$);
	else if (.@size == 1) set @shop_index, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set @shop_index, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[@shop_index] == "") {
		message strcharinfo(0),"An error has occurred.";
		end;
	}
	callshop "instanceshop"+@shop_index,1;
	npcshopattach "instanceshop"+@shop_index;
	end;

OnBuyItem:
	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+@shop_index+"_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+@shop_index+"_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"You can't purchase that many "+getitemname(.@q[0])+".";
		end;
	}
	mes "[^FF6600Merchant Master^000000]";
	mes "Reward: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "Requirements:";
	disable_items;
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0], ITEMINFO_LOCATIONS), getiteminfo(.@q[0], ITEMINFO_VIEW);
	if (@qe[2] > 0 && ((@qe[1] & EQP_HEAD_LOW) || (@qe[1] & EQP_HEAD_TOP) || (@qe[1] & EQP_HEAD_MID) || (@qe[1] & EQP_COSTUME_HEAD_TOP) || (@qe[1] & EQP_COSTUME_HEAD_MID) || (@qe[1] & EQP_COSTUME_HEAD_LOW) || (@qe[1] & EQP_GARMENT) || (@qe[1] & EQP_COSTUME_GARMENT)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" ~ Purchase ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[7])?" ~ Preview...":"")+": ~ ^777777Cancel^000000")) {
		case 1:
			if (@qe[0]) {
				mes "[^FF6600Merchant Master^000000]";
				mes "You're missing one or more quest requirements.";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[^FF6600Merchant Master^000000]";
				mes "^FF0000You need "+(((.@q[2]*getiteminfo(.@q[0], ITEMINFO_WEIGHT))+Weight-MaxWeight)/10)+" additional weight capacity to complete this trade.^000000";
				close;
			}
			if (.@q[4]) set Zeny, Zeny-(.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[2];
			if (.Announce) announce strcharinfo(0)+" has created "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):callfunc("F_InsertArticle",getitemname(.@q[0])))+"!",0;
			specialeffect2 EF_FLOWERLEAF;
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {
	if (getitemname(getarg(1)) == "null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Quest requirement #"+getarg(.@i)+" invalid (skipped).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(0)+"_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "instanceshop"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^00FF00";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0), ITEMINFO_TYPE) == 4 || getiteminfo(getarg(0), ITEMINFO_TYPE) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}
}

function	script	instanceshop	{
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "instance_shop::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	instanceshop1	-1,909:-1
-	shop	instanceshop2	-1,909:-1
-	shop	instanceshop3	-1,909:-1
-	shop	instanceshop4	-1,909:-1
-	shop	instanceshop5	-1,909:-1
-	shop	instanceshop6	-1,909:-1
-	shop	instanceshop7	-1,909:-1
-	shop	instanceshop8	-1,909:-1
-	shop	instanceshop9	-1,909:-1
-	shop	instanceshop10	-1,909:-1
-	shop	instanceshop11	-1,909:-1
-	shop	instanceshop12	-1,909:-1