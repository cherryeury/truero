//===== rAthena Script ======================================= 
//= Malangdo Enchants
//===== By: ================================================== 
//= Muad_Dib
//===== Current Version: ===================================== 
//= 1.0.3
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Adds enchantments to many high level weapons in exchange
//= for Malangdo coins.
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Euphy]
//= 1.0.1 Added 'delequip' command. [Euphy]
//= 1.0.2 Remove redundant usage of 'getarg' [Cydh]
//= 1.0.3 Fixed typo in 1.0.2 [Dastgir]
//============================================================ 

// Enchanter NPC :: mal_yong
//============================================================
malangdo,215,166,4	script	Meong#mal	545,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "You have too many kinds of objects. Let's try to continue after reducing those objects.";
		close;
	}
	if (MaxWeight - Weight < 10000) {
		mes "Can't continue because you have too many heavy objects. Let's try to continue after reducing the weight.";
		close;
	}
	mes "[Meong]";
	mes "Wow welcome. It is new Meong refinement store~ I enchant some luxury slotted Daggers.";
	mes "But i can only enchant it once, unlike my sister mayomayo.";
	next;
	switch( select( "Enchant High Ranked Weapon", "Initialize Enchant (Need Silvervine Fruit)" ) ) {
	case 1:
		@mal_enchant_select = 1;
		break;
	case 2:
		@mal_enchant_select = 2;
		break;
	}

	if (!getequipisequiped(EQI_HAND_R)) {
		mes "[Meong]";
		mes "Did you take off your equipment?";
		@mal_enchant_select = 0;
		close;
	}
	@mal_equip_id = getequipid(EQI_HAND_R);
	.@equip_type = getiteminfo(@mal_equip_id, ITEMINFO_VIEW);

	// callsub L_Socket,<cost multiplier>,<4-x enchants possible>;
	switch(.@equip_type) {	//  Check weapon type first to speed up the checks.
	case 1:	// Daggers
		switch(@mal_equip_id) {
			case 13016: callsub L_Socket,1,2;	// Poison_Knife
			case 13097: callsub L_Socket,1,2;	// Grimtooth__
			case 13018: callsub L_Socket,1,2;	// Sucsamad_
			case 13019: callsub L_Socket,1,2;	// Ginnungagap_
			case 13052: callsub L_Socket,1,2;	// Holy_Dagger_
			case 13051: callsub L_Socket,1,2;	// Mama's_Knife_
			case 13033: callsub L_Socket,1,2;	// Assassin_Dagger_
			case 13021: callsub L_Socket,2,2;	// Combat_Knife
			case 13030: callsub L_Socket,2,2;	// Dragon_Killer_
			case 13054: callsub L_Socket,2,2;	// Exorcizer_
			case 13029: callsub L_Socket,2,2;	// Weeder_Knife_
			case 13083: callsub L_Socket,2,2;	// Princess_Knife_
			case 13017: callsub L_Socket,4,2;	// House_Auger			
		}
		break;
	}
	mes "[Meong]";
	if (@mal_enchant_select == 1)
		mes "This is not a suitable equipment for the enchant. Don't forget we only take care of high class weapons, including level 4 weapons~";
	else
		mes "This equipment can not be initialized. Don't forget we only handle some luxury weapons, including level 4 weapons~";
	@mal_equip_id = 0;
	@mal_enchant_select = 0;
	close;

L_Socket:
	.@select = @mal_enchant_select;
	.@equip_id = @mal_equip_id;
	.@equip_refine = getequiprefinerycnt(EQI_HAND_R);
	.@equip_name$ = getitemname(.@equip_id) + ( getitemslots(.@equip_id) ? "[" + getitemslots(.@equip_id) + "]" : "" );
	setarray .@equip_card[0], getequipcardid(EQI_HAND_R,0), getequipcardid(EQI_HAND_R,1), getequipcardid(EQI_HAND_R,2), getequipcardid(EQI_HAND_R,3);
	@mal_equip_id = 0;
	@mal_enchant_select = 0;

	if (.@select == 1) {
		mes "[Meong]";
		mes "^0000ff" + .@equip_name$ + "^000000! Do you want to enchant this equipment? How will you pay for this?";
		next;

		.@enccost = getarg(0);
		.@enclimit = getarg(1);
		setarray .@coin[0],6422,6421,6420,6419,6418,6423;	//  Payment ID
		setarray .@cost[0],  15,  10,   6,   3,   2,   1;	//  Payment multiplier

		.@menu$ = "Stop:";
		.@size_coin = getarraysize(.@coin);
		for ( .@i = 0; .@i < .@size_coin; ++.@i) {
			.@count[.@i] = countitem(.@coin[.@i]);
			.@total[.@i] = .@enccost * .@cost[.@i];
			if (.@count[.@i] < .@total[.@i])
				.@menu$ += "^999999" + getitemname(.@coin[.@i]) + " (missing " + (.@total[.@i]-.@count[.@i]) + ")^000000:";
			else
				.@menu$ += getitemname(.@coin[.@i]) + " (have " + .@count[.@i] + ", need " + .@total[.@i] + "):";
		}
		.@coin_select = select(.@menu$) - 2;
		if (.@coin_select == -1) {
			mes "[Meong]";
			mes "Come back again if you change your mind.";
			close;
		} else if (.@count[.@coin_select] < .@total[.@coin_select]) {
			mes "[Meong]";
			mes "You don't have enough coins. Do you want to check again?";
			close;
		}
		switch(.@coin_select) {
		case 0:	// Egrade_Coin
			.@enchant_type = 10;
			break;
		case 1:	// Dgrade_Coin
			.@enchant_type = 9;
			break;
		case 2:	// Cgrade_Coin
			.@enchant_type = 8;
			break;
		case 3:	// Bgrade_Coin
			.@enchant_type = 7;
			break;
		case 4:	// Agrade_Coin
		case 5:	// Anger_Seagod
			mes "[Meong]";
			mes "When you use the " + getitemname(.@coin[.@coin_select]) + ", you can choose the enchant type.";
			next;
			switch( select( "Stop", "Short Range Type", "Long Range Type", "Caster Type") ) {
			case 1:
				mes "[Meong]";
				mes "Ok. If you change your mind, let me know.";
				close;
			case 2:
				.@enchant_type = ((.@coin_select == 4) ? 4 : 1);
				break;
			case 3:
				.@enchant_type = ((.@coin_select == 4) ? 5 : 2);
				break;
			case 4:
				.@enchant_type = ((.@coin_select == 4) ? 6 : 3);
				break;
			}
			break;
		}
		if (.@equip_card[3] == 0 && .@enclimit < 4) {
			.@socket = 4;
			.@str$ = "1st";
//		} else if (.@equip_card[2] == 0 && .@enclimit < 3) {
//			.@socket = 3;
//			.@str$ = "2nd";
		} else {
			mes "[Meong]";
			mes "This equipment is at the end of enchant. Please initialize the enchant and you will be able to enchant it again, or bring another weapon.";
			close;
		}
		mes "[Meong]";
		mes "I'm trying for the " + .@str$ + " enchant. ^ff0000It will not touch previous refinement and cards. Also, the equipment will not be destroyed.^000000 Can I continue?";
		next;
		if (select( "I'll come back later.", "Please, continue." ) == 1) {
			mes "[Meong]";
			mes "Ok. If you change your mind, come back again.";
			close;
		}
		switch(.@enchant_type) {
		case 10:	// Egrade_Coin
			.@i = rand(1,531);
			     if (.@i < 101) .@enchant = 4787;	// Mdef4
			else if (.@i < 201) .@enchant = 4792;	// Def6
			else if (.@i < 301) .@enchant = 4801;	// SP100
			else if (.@i < 351) .@enchant = 4795;	// HP100
			else if (.@i < 401) .@enchant = 4796;	// HP200
			else if (.@i < 451) .@enchant = 4819;	// Atk1
			else if (.@i < 476) .@enchant = 4720;	// Dexterity1
			else if (.@i < 501) .@enchant = 4740;	// Vitality1
			else if (.@i < 526) .@enchant = 4750;	// Luck1
			else if (.@i < 528) .@enchant = 4700;	// Strength1
			else if (.@i < 530) .@enchant = 4730;	// Agility1
			else if (.@i < 532) .@enchant = 4710;	// Inteligence1
			else .@enchant = 9;
			break;
		case 9:	// Dgrade_Coin
			.@i = rand(1,531);
			     if (.@i < 101) .@enchant = 4795;	// HP100
			else if (.@i < 201) .@enchant = 4796;	// HP200
			else if (.@i < 301) .@enchant = 4819;	// Atk1
			else if (.@i < 351) .@enchant = 4720;	// Dexterity1
			else if (.@i < 401) .@enchant = 4740;	// Vitality1
			else if (.@i < 451) .@enchant = 4750;	// Luck1
			else if (.@i < 476) .@enchant = 4700;	// Strength1
			else if (.@i < 501) .@enchant = 4730;	// Agility1
			else if (.@i < 526) .@enchant = 4710;	// Inteligence1
			else if (.@i < 528) .@enchant = 4701;	// Strength2
			else if (.@i < 530) .@enchant = 4731;	// Agility2
			else if (.@i < 532) .@enchant = 4711;	// Inteligence2
			else .@enchant = 9;
			break;
		case 8:	// Cgrade_Coin
			.@i = rand(1,531);
			     if (.@i < 101) .@enchant = 4720;	// Dexterity1
			else if (.@i < 201) .@enchant = 4740;	// Vitality1
			else if (.@i < 301) .@enchant = 4750;	// Luck1
			else if (.@i < 351) .@enchant = 4700;	// Strength1
			else if (.@i < 401) .@enchant = 4730;	// Agility1
			else if (.@i < 451) .@enchant = 4710;	// Inteligence1
			else if (.@i < 476) .@enchant = 4701;	// Strength2
			else if (.@i < 501) .@enchant = 4731;	// Agility2
			else if (.@i < 526) .@enchant = 4711;	// Inteligence2
			else if (.@i < 528) .@enchant = 4702;	// Strength3
			else if (.@i < 530) .@enchant = 4732;	// Agility3
			else if (.@i < 532) .@enchant = 4712;	// Inteligence3
			else .@enchant = 9;
			break;
		case 7:	// Bgrade_Coin
			.@i = rand(1,531);
			     if (.@i < 101) .@enchant = 4700;	// Strength1
			else if (.@i < 201) .@enchant = 4730;	// Agility1
			else if (.@i < 301) .@enchant = 4710;	// Inteligence1
			else if (.@i < 351) .@enchant = 4701;	// Strength2
			else if (.@i < 401) .@enchant = 4731;	// Agility2
			else if (.@i < 451) .@enchant = 4711;	// Inteligence2
			else if (.@i < 476) .@enchant = 4702;	// Strength3
			else if (.@i < 501) .@enchant = 4732;	// Agility3
			else if (.@i < 526) .@enchant = 4712;	// Inteligence3
			else if (.@i < 528) .@enchant = 4703;	// Strength4
			else if (.@i < 530) .@enchant = 4733;	// Agility4
			else if (.@i < 532) .@enchant = 4713;	// Inteligence4
			else .@enchant = 9;
			break;
		case 6:	// Agrade_Coin - Caster
			.@i = rand(1,555);
			     if (.@i < 81)  .@enchant = 4711;	// Inteligence2
			else if (.@i < 161) .@enchant = 4721;	// Dexterity2
			else if (.@i < 241) .@enchant = 4814;	// Spell2
			else if (.@i < 311) .@enchant = 4712;	// Inteligence3
			else if (.@i < 371) .@enchant = 4722;	// Dexterity3
			else if (.@i < 431) .@enchant = 4813;	// Spell3
			else if (.@i < 476) .@enchant = 4713;	// Inteligence4
			else if (.@i < 516) .@enchant = 4812;	// Spell4
			else if (.@i < 526) .@enchant = 4760;	// Matk1
			else if (.@i < 546) .@enchant = 4714;	// Inteligence5
			else if (.@i < 551) .@enchant = 4826;	// Spell5
			else if (.@i < 556) .@enchant = 4761;	// Matk2
			else .@enchant = 9;
			break;
		case 5:	// Agrade_Coin - Long Range
			.@i = rand(1,555);
			     if (.@i < 81)  .@enchant = 4731;	// Agility2
			else if (.@i < 161) .@enchant = 4833;	// Expert_Archer2
			else if (.@i < 241) .@enchant = 4817;	// Sharp2
			else if (.@i < 311) .@enchant = 4732;	// Agility3
			else if (.@i < 371) .@enchant = 4834;	// Expert_Archer3
			else if (.@i < 431) .@enchant = 4816;	// Sharp3
			else if (.@i < 476) .@enchant = 4733;	// Agility4
			else if (.@i < 516) .@enchant = 4835;	// Expert_Archer4
			else if (.@i < 526) .@enchant = 4807;	// Atk_Speed1
			else if (.@i < 546) .@enchant = 4734;	// Agility5
			else if (.@i < 551) .@enchant = 4836;	// Expert_Archer5
			else if (.@i < 556) .@enchant = 4807;	// Atk_Speed1
			else .@enchant = 9;
			break;
		case 4:	// Agrade_Coin - Short Range
			.@i = rand(1,555);
			     if (.@i < 81)  .@enchant = 4731;	// Agility2
			else if (.@i < 161) .@enchant = 4808;	// Fighting_Spirit4
			else if (.@i < 241) .@enchant = 4817;	// Sharp2
			else if (.@i < 311) .@enchant = 4732;	// Agility3
			else if (.@i < 371) .@enchant = 4820;	// Fighting_Spirit5
			else if (.@i < 431) .@enchant = 4816;	// Sharp3
			else if (.@i < 476) .@enchant = 4733;	// Agility4
			else if (.@i < 516) .@enchant = 4821;	// Fighting_Spirit6
			else if (.@i < 526) .@enchant = 4807;	// Atk_Speed1
			else if (.@i < 546) .@enchant = 4734;	// Agility5
			else if (.@i < 551) .@enchant = 4822;	// Fighting_Spirit7
			else if (.@i < 556) .@enchant = 4807;	// Atk_Speed1
			else .@enchant = 9;
			break;
		case 3:	// Anger_Seagod - Caster
			.@i = rand(1,555);
			     if (.@i < 81)  .@enchant = 4712;	// Inteligence3
			else if (.@i < 161) .@enchant = 4722;	// Dexterity3
			else if (.@i < 241) .@enchant = 4813;	// Spell3
			else if (.@i < 311) .@enchant = 4713;	// Inteligence4
			else if (.@i < 371) .@enchant = 4812;	// Spell4
			else if (.@i < 431) .@enchant = 4760;	// Matk1
			else if (.@i < 476) .@enchant = 4714;	// Inteligence5
			else if (.@i < 516) .@enchant = 4826;	// Spell5
			else if (.@i < 526) .@enchant = 4761;	// Matk2
			else if (.@i < 546) .@enchant = 4715;	// Inteligence6
			else if (.@i < 551) .@enchant = 4827;	// Spell6
			else if (.@i < 556) .@enchant = 4761;	// Matk2
			else .@enchant = 9;
			break;
		case 2:	// Anger_Seagod - Long Range
			.@i = rand(1,555);
			     if (.@i < 81)  .@enchant = 4732;	// Agility3
			else if (.@i < 161) .@enchant = 4834;	// Expert_Archer3
			else if (.@i < 241) .@enchant = 4843;	// Sharp4
			else if (.@i < 311) .@enchant = 4733;	// Agility4
			else if (.@i < 371) .@enchant = 4835;	// Expert_Archer4
			else if (.@i < 431) .@enchant = 4844;	// Sharp5
			else if (.@i < 476) .@enchant = 4734;	// Agility5
			else if (.@i < 516) .@enchant = 4836;	// Expert_Archer5
			else if (.@i < 526) .@enchant = 4807;	// Atk_Speed1
			else if (.@i < 546) .@enchant = 4735;	// Agility6
			else if (.@i < 551) .@enchant = 4837;	// Expert_Archer6
			else if (.@i < 556) .@enchant = 4807;	// Atk_Speed1
			else .@enchant = 9;
			break;
		case 1:	// Anger_Seagod - Short Range
			.@i = rand(1,555);
			     if (.@i < 81)  .@enchant = 4732;	// Agility3
			else if (.@i < 161) .@enchant = 4820;	// Fighting_Spirit5
			else if (.@i < 241) .@enchant = 4843;	// Sharp4
			else if (.@i < 311) .@enchant = 4733;	// Agility4
			else if (.@i < 371) .@enchant = 4821;	// Fighting_Spirit6
			else if (.@i < 431) .@enchant = 4844;	// Sharp5
			else if (.@i < 476) .@enchant = 4734;	// Agility5
			else if (.@i < 516) .@enchant = 4822;	// Fighting_Spirit7
			else if (.@i < 526) .@enchant = 4807;	// Atk_Speed1
			else if (.@i < 546) .@enchant = 4735;	// Agility6
			else if (.@i < 551) .@enchant = 4823;	// Fighting_Spirit8
			else if (.@i < 556) .@enchant = 4807;	// Atk_Speed1
			else .@enchant = 9;
			break;
		default:
			mes "[Meong]";
			mes "There is something wrong. Please try again.";
			close;
		}

		// anti-hack
		if (callfunc("F_IsEquipIDHack", EQI_HAND_R, .@equip_id) || callfunc("F_IsEquipRefineHack", EQI_HAND_R, .@equip_refine) || 
		    callfunc("F_IsEquipCardHack", EQI_HAND_R, .@equip_card[0], .@equip_card[1], .@equip_card[2], .@equip_card[3]))
			close;

		     if (.@equip_card[3] == 0 && .@enclimit < 4) .@equip_card[3] = .@enchant;
		else if (.@equip_card[2] == 0 && .@enclimit < 3) .@equip_card[2] = .@enchant;
		else if (.@equip_card[1] == 0 && .@enclimit < 2) .@equip_card[1] = .@enchant;
		else if (.@equip_card[0] == 0 && .@enclimit < 1) .@equip_card[0] = .@enchant;
		else {
			mes "[Meong]";
			mes "This equipment is at the end of enchant. I provide enchant for two times maximum.";
			next;
			mes "[Meong]";
			mes "Surely I don't enchant at card socket, therefore weapons that have 3 slots can be enchanted only 1 time. Don't forget this~";
			close;
		}
		if (.@enchant == 9) {	//  Should never happen.
			specialeffect2 EF_SUI_EXPLOSION;
			mes "[Meong]";
			mes "Oh my god!";
			mes "This equipment is destroyed because it could not endure powerful ability. I'm so sorry.";
			delitem .@coin[.@coin_select],.@total[.@coin_select];
			delequip EQI_HAND_R;
			close;
		}
		specialeffect2 EF_REPAIRWEAPON;
		mes "[Meong]";
		mes "I have enchanted ^990000slot " + .@socket + "^000000 of this equipment.";
		delitem .@coin[.@coin_select], .@total[.@coin_select];
		delequip EQI_HAND_R;

		// GetNonSlotItemSock2 .@equip_refine .@equip_id .@equip_card[0] .@equip_card[1] .@equip_card[2] .@equip_card[3]
		getitem2 .@equip_id,1,1,.@equip_refine,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],.@equip_card[3];

		close;
	} else if (.@select == 2) {
		mes "[Meong]";
		mes "I will only initialize the enchant effect without touching previous refined level and cards.";
		next;
		if (select( "Stop", "Continue" ) == 1) {
			mes "[Meong]";
			mes "If you change your mind, come back anytime.";
			close;
		}
		if (countitem(6417) == 0) {
			mes "[Meong]";
			mes "I'm sorry. You don't have Silvervine Fruit. Please check your inventory again?";
			close;
		}
		if (!getequipisequiped(EQI_HAND_R)) {
			mes "[Meong]";
			mes "Did you take off the equipment?";
			close;
		}
		if (.@equip_card[3] == 0) {
			mes "[Meong]";
			mes "This equipment has nothing to initialize. Please check again.";
			close;
		}
		specialeffect2 EF_REPAIRWEAPON;
		mes "[Meong]";
		mes "Initialize the enchant effect from the equipment.";
		delitem 6417,1;	// Silvervine

		// anti-hack
		if (callfunc("F_IsEquipIDHack", EQI_HAND_R, .@equip_id) || callfunc("F_IsEquipRefineHack", EQI_HAND_R, .@equip_refine) || 
		    callfunc("F_IsEquipCardHack", EQI_HAND_R, .@equip_card[0], .@equip_card[1], .@equip_card[2], .@equip_card[3]))
			close;

		delequip EQI_HAND_R;

		// GetNonSlotItemSock2 .@equip_refine .@equip_id .@equip_card[0] .@equip_card[1] .@equip_card[2] .@equip_card[3]
		for ( .@i = getiteminfo(.@equip_id, ITEMINFO_SLOT); .@i < MAX_SLOTS; .@i++ ) {
			if (callfunc("F_IsCharm",.@equip_card[.@i]) == true)
				.@equip_card[.@i] = 0;// Armor Enchant System
		}
		getitem2 .@equip_id,1,1,.@equip_refine,0,.@equip_card[0],.@equip_card[1],.@equip_card[2],.@equip_card[3];

		close;
	}
}
