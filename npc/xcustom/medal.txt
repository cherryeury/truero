// NPCs for Redeeming Prize Medals (Einbroch and Hugel)
//============================================================
ein_in01,87,208,5	script	Ei'felle#repay01	854,{
	if (!ein_medal01) {
		mes "[Ei'felle]";
		mes "Curses! We need to deliver";
		mes "what our customers ordered,";
		mes "but we've been making nothing";
		mes "but shipshod products! If we";
		mes "only had that metal, we could";
		mes "pump up our product quality!";
		next;
		select("What metal are you talking about?");
		mes "[Ei'felle]";
		mes "There's a small village";
		mes "at the outskirts of the";
		mes "Schwarzwald Republic that";
		mes "gives these special medals";
		mes "that are made of this metal";
		mes "which we desperately need...";
		next;
		mes "[Ei'felle]";
		mes "We've sent one of our best";
		mes "guildsmen to get some of those";
		mes "medals for us, but he hasn't";
		mes "reported back to us quite yet.";
		mes "I'm getting pretty anxious...";
		next;
		mes "[Ei'felle]";
		mes "I mean, we need a whole";
		mes "lot of that metal to fill out";
		mes "our orders and finish our";
		mes "manufacturing research,";
		mes "but so far, none of us have";
		mes "been able to get any medals...";
		next;
		if (countitem("Marvelous_Medal")) {//7515
			mes "["+ strcharinfo(0) +"]";
			mes "Oh! Are you talking";
			mes "about the medals that";
			mes "they give as rewards in";
			mes "the Monster Race Arena?";
			mes "I have some of those.";
			next;
			mes "[Ei'felle]";
			mes "Huh? Show them to me...";
			mes "Yes! That's exactly what";
			mes "we need! Would you please";
			mes "donate your medals so that we";
			mes "can finally make some quality";
			mes "products for our customers?";
			next;
			callsub S_Reward;
			end;
		}
		mes "[Ei'felle]";
		mes "If you happen to obtain any";
		mes "medals from the Monster";
		mes "Race Arena in Hugel, then";
		mes "please bring some of them";
		mes "to me. I'll be sure to repay";
		mes "you for your kindness...";
		close;
	} else if (ein_medal01 < 500) {
		mes "[Ei'felle]";
		mes "Oh, how have you been?";
		mes "Thank you so much for";
		mes "donating so many medals,";
		mes "they've been helpful in my";
		mes "research. Still, I need more";
		mes "and more of them everyday...";
		next;
		if (countitem("Marvelous_Medal")) {
			mes "[Ei'felle]";
			mes "The other Blacksmith";
			mes "Guildsmen are doing their";
			mes "best to collect Prize Medals";
			mes "in Hugel, but they keep failing";
			mes "to win them! If you have any";
			mes "medals, then may I have some?";
			next;
			callsub S_Reward;
			end;
		}
		mes "[Ei'felle]";
		mes "If you happen to obtain any";
		mes "medals from the Monster";
		mes "Race Arena in Hugel, then";
		mes "please bring some of them";
		mes "to me. I'll be sure to repay";
		mes "you for your kindness...";
		close;
	}
	if (!checkweight("Knife",1)) {//1201
		mes "[Ei'felle]";
		mes "Goodness, you're carrying";
		mes "so many things with you!";
		mes "You'd better put some of";
		mes "it away in Kafra Storage";
		mes "before you're overwhelmed";
		mes "by the bulk of your items!";
		close;
	}
	if (ein_medal01 > 499 && ein_medal01 < 1500) {
		if (ein_medal01 < 1000) {
			mes "[Ei'felle]";
			mes "Ah, welcome back! I've finally";
			mes "made a breakthrough in my";
			mes "metal research! Look, I've";
			mes "developed this Glittering";
			mes "Jacket! It's lightweight and";
			mes "very durable, you see?";
			next;
			mes "[Ei'felle]";
			mes "I wouldn't be able to have";
			mes "completed this without your";
			mes "help. Now, would you like to";
			mes "receive this Glittering Jacket";
			mes "as my way of repaying you?";
			next;
			if (select("Accept:Wait for Further Development") == 1) {
				callsub S_BonusReward,500,2319;//Glittering_Clothes
				end;
			}
			mes "[Ei'felle]";
			mes "Ah, I see. You'd rather wait";
			mes "until we develop something";
			mes "more to your liking. In that";
			mes "case, would you please donate";
			mes "more medals to my research?";
			mes "We're always low on them...";
			next;
		} else if (ein_medal01 < 1500) {
			mes "[Ei'felle]";
			mes "Oh, you're back!";
			mes "Thanks to all the medals";
			mes "that you've donated, I'm now";
			mes "able to manufacture a set";
			mes "of slotted armor imbued";
			mes "with a property of your choice.";
			next;
			mes "[Ei'felle]";
			mes "If you'd like, I can repay you";
			mes "now by creating a set of slotted elemental armor for you, or we";
			mes "can wait for you to donate more";
			mes "medals until I can develop";
			mes "something else for you.";
			next;
			switch(select("Accept Armor:Can I have something else?:Wait for Further Development")) {
				case 1:
					mes "[Ei'felle]";
					mes "I can manufacture one set of";
					mes "slotted Armor imbued with the";
					mes "Fire, Earth, Wind, or Water";
					mes "property. Which property would";
					mes "you like your armor to have?";
					next;
					switch(select("Fire Property:Earth Property:Wind Property:Water Property")) {
						case 1: callsub S_BonusReward,1000,2345; end;// Flame_Sprits_Armor_
						case 2: callsub S_BonusReward,1000,2351; end;// Earth_Sprits_Armor_
						case 3: callsub S_BonusReward,1000,2349; end;// Wind_Sprits_Armor_
						case 4: callsub S_BonusReward,1000,2347; end;// Water_Sprits_Armor_
					}
				case 2:
					mes "[Ei'felle]";
					mes "Something else...?";
					mes "Oh, you must mean";
					mes "the Glittering Jacket that";
					mes "I developed earlier. After";
					mes "all, I have any other items";
					mes "to offer you for now...";
					next;
					callsub S_BonusReward,500,2319;
					end;
				case 3:
					mes "[Ei'felle]";
					mes "Ah, I see. You'd rather wait";
					mes "until we develop something";
					mes "more to your liking. In that";
					mes "case, would you please donate";
					mes "more medals to my research?";
					mes "We're always low on them...";
					next;
					break;
			}
		}
		if (countitem("Marvelous_Medal")) {
			mes "[Ei'felle]";
			mes "The other Blacksmith";
			mes "Guildsmen are doing their";
			mes "best to collect Prize Medals";
			mes "in Hugel, but they keep failing";
			mes "to win them! If you have any";
			mes "medals, then may I have some?";
			next;
			callsub S_Reward;
			end;
		}
		mes "[Ei'felle]";
		mes "If you happen to obtain any";
		mes "medals from the Monster";
		mes "Race Arena in Hugel, then";
		mes "please bring some of them";
		mes "to me. I'll be sure to repay";
		mes "you for your kindness...";
		close;
	} else if (ein_medal01 > 1499) {
		mes "[Ei'felle]";
		mes "Ah, you're back! I've";
		mes "extracted all the metal";
		mes "from the medals you've";
		mes "given me, and I think I have";
		mes "enough to create a Level 4";
		mes "Weapon. Isn't that incredible?";
		next;
		mes "[Ei'felle]";
		mes "If you like, I can create";
		mes "one of these weapons for you";
		mes "as my way of repaying you for";
		mes "your help. What do you think?";
		next;
		if (select("Yes, I want a Level 4 Weapon.:Can I have something else?") == 2) {
			mes "[Ei'felle]";
			mes "Something else?";
			mes "Oh, alright then, would";
			mes "you like to have a Glittering";
			mes "Jacket, or a set of slotted";
			mes "elemental Armor? Please go";
			mes "ahead and make your choice~";
			next;
			switch (select("Glittering Jacket:Fire Property Armor:Earth Property Armor:Wind Property Armor:Water Property Armor:Cancel")) {
				case 1: callsub S_BonusReward,500,2319; end;
				case 2: callsub S_BonusReward,1000,2345; end;
				case 3: callsub S_BonusReward,1000,2351; end;
				case 4: callsub S_BonusReward,1000,2349; end;
				case 5: callsub S_BonusReward,1000,2347; end;
				case 6: end;
			}
		}
		setarray .@type$, "Dagger","One Handed Sword","Two Handed Sword","Axe","Mace","Bow","Staff","Book","Spear","Katar","Knuckle","Whip","Musical Instrument";
		.@m = select( implode(.@type$,":") ) - 1;
		mes "[Ei'felle]";
		mes "So you'd like to have a";
		mes .@type$[.@m]+"? Please choose";
		mes "which Level 4 Weapon";
		mes "that you want me to create.";
		next;
		switch(.@m) {
			case 0:
				.@weapon$ = "Ginnungagap:Grimtooth:Dragon Killer:Mail Breaker:Bazerald:Sword Breaker:Ice Pick:Sucsamad:Kitchen Knife:Azoth:Exorciser:Assassin Dagger:Moonlight Dagger:Weeder Knife:Cursed Dagger:Dagger of Counter:Combat Knife:Fortune Sword";
				setarray .@weapon_id, 13002,1237,13001,1225,1231,1224,1230,1236,1229,1235,1233,1232,1234,1227,1241,1242,1228,1223;
				break;
			case 1:
				.@weapon$ = "Nagan:Immaterial Sword:Mysteltainn:Byeollungum:Star Dust Blade:Caesar's Sword:Ice Falchion:Excalibur:Edge:Cutlus:Solar Sword:Tirfing:Fireblend";
				setarray .@weapon_id, 1130,1141,1138,1140,1148,1134,1131,1137,1132,1135,1136,1139,1133;
				break;
			case 2:	
				.@weapon$ = "Dragon Slayer:Masamune:Muramasa:Schweizersabel:Executioner:Zweihander:Katzbalger";
				setarray .@weapon_id, 1166,1165,1164,1167,1169,1168,1170;
				break;
			case 3:
				.@weapon$ = "Great Axe:Guillotine:Light Epsilon:Bloody Axe:Sabbath:Slaughter:Cleaver:Tomahawk";
				setarray .@weapon_id, 1364,1369,1366,1363,1365,1367,1305,1368;
				break;
			case 4:
				.@weapon$ = "Golden Mace:Grand Cross:Long Mace:Spike:Slash:Quadrille";
				setarray .@weapon_id, 1524,1528,1525,1523,1526,1527;
				break;
			case 5:
				.@weapon$ = "Roguemaster's Bow:Dragon Wing:Rudra's Bow:Ballista";
				setarray .@weapon_id, 1719,1724,1720,1722;
				break;
			case 6:
				.@weapon$ = "Wing Staff:Wizardry Staff";
				setarray .@weapon_id, 1616,1473;
			case 7:
				.@weapon$ = "Legacy of Dragon:Book of the Apocalypse:Girl's Diary:Hardcover Book";
				setarray .@weapon_id, 1559,1557,1558,1561;
				break;
			case 8:
				.@weapon$ = "Gae Bolg:Gelerdria:Gungnir:Skewer:Longinus's Spear:Brionac:Bill Guisarme:Zephyrus:Crescent Scythe:Tjungkuletti:Hellfire";
				setarray .@weapon_id, 1474,1414,1413,1415,1469,1470,1467,1468,1466,1416,1471;
				break;
			case 9:
				.@weapon$ = "Infiltrator:Bloody Roar:Unholy Touch";
				setarray .@weapon_id, 1261,1265,1263;
				break;
			case 10:
				.@weapon$ = "Hatii Claw:Berserk:Kaiser Knuckle";
				setarray .@weapon_id, 1815,1814,1813;
				break;
			case 11:
				.@weapon$ = "Lariat:Rapture Rose:Blade Whip:Chemeti:Queen's Whip";
				setarray .@weapon_id, 1962,1963,1969,1964,1970;
				break;
			case 12:
				.@weapon$ = "Oriental Lute:Electric Guitar";
				setarray .@weapon_id, 1918,1913;
				break;		
		}
		.@j = select (.@weapon$+":Cancel") - 1;
		explode(.@weapon$,.@weapon$,":");
		if (.@j == getarraysize(.@weapon$)) close;
		callsub S_BonusReward,1500,.@weapon_id[.@j];
		end;
	}
	
S_Reward:
	if (select("Sure:No") == 2) {
		mes "[Ei'felle]";
		mes "Oh, alright...";
		mes "Still, I really need";
		mes "those medals, so if you";
		mes "change your mind, please";
		mes "come back as soon as you can.";
		emotion ET_CRY;
		close;
	}
	mes "[Ei'felle]";
	mes "Oh, thank you so much!";
	mes "I can use the metal in those";
	mes "medals to produce some high";
	mes "quality products. Now, how";
	mes "shall I repay you for giving me";
	mes "some of your Prize Medals?";
	next;
	switch(select("Base Experience:Job Experience:No, I'm just glad to help.:How's your research progressing?")) {
		case 1:
			mes "[Ei'felle]";
			mes "You just want to improve";
			mes "yourself, huh? Well, I dunno";
			mes "if you want to get stronger,";
			mes "smarter, or faster, but I can";
			mes "coach you on some visualization if you like. Now, relax with me~";
			mes "physical development.";
			next;
			mes "[Ei'felle]";
			mes "Focus... and believe.";
			mes "Believe that you are";
			mes "becoming what you want";
			mes "to be! B-believe... with";
			mes "all of your freakin' heart!";
			emotion ET_HUK, playerattached();
			emotion ET_HUK;
			next;
			mes "[Ei'felle]";
			mes "*Phew* How's that?";
			mes "Now do you see the";
			mes "power of hope?";
			delitem 7515,1; //Marvelous_Medal
			if (checkre(3)) {
				if (BaseLevel < 21) getexp 110,0;
				else if ((BaseLevel > 20) && (BaseLevel < 31)) getexp 280,0;
				else if ((BaseLevel > 30) && (BaseLevel < 41)) getexp 1000,0;
				else if ((BaseLevel > 40) && (BaseLevel < 51)) getexp 1200,0;
				else if ((BaseLevel > 50) && (BaseLevel < 61)) getexp 1500,0;
				else if ((BaseLevel > 60) && (BaseLevel < 71)) getexp 2800,0;
				else if ((BaseLevel > 70) && (BaseLevel < 81)) getexp 4500,0;
				else getexp 8500,0;

			/* iRO EXP Table:
				if (BaseLevel < 21) getexp 200,0;
				else if ((BaseLevel > 20) && (BaseLevel < 31)) getexp 300,0;
				else if ((BaseLevel > 30) && (BaseLevel < 41)) getexp 1000,0;
				else if ((BaseLevel > 40) && (BaseLevel < 51)) getexp 1800,0;
				else if ((BaseLevel > 50) && (BaseLevel < 61)) getexp 2500,0;
				else if ((BaseLevel > 60) && (BaseLevel < 71)) getexp 4700,0;
				else if ((BaseLevel > 70) && (BaseLevel < 81)) getexp 5500,0;
				else if ((BaseLevel > 80) && (BaseLevel < 96)) getexp 7000,0;
				else if ((BaseLevel > 95) && (BaseLevel < 111)) getexp 8800,0;
				else if ((BaseLevel > 110) && (BaseLevel < 126)) getexp 10900,0;
				else if ((BaseLevel > 125) && (BaseLevel < 141)) getexp 13300,0;
				else getexp 16000,0;
			*/

			} else {
				if (BaseLevel < 21) getexp 150,0;
				else if ((BaseLevel > 20) && (BaseLevel < 31)) getexp 300,0;
				else if ((BaseLevel > 30) && (BaseLevel < 41)) getexp 2000,0;
				else if ((BaseLevel > 40) && (BaseLevel < 51)) getexp 8000,0;
				else if ((BaseLevel > 50) && (BaseLevel < 61)) getexp 25000,0;
				else if ((BaseLevel > 60) && (BaseLevel < 71)) getexp 47000,0;
				else if ((BaseLevel > 70) && (BaseLevel < 81)) getexp 55000,0;
				else getexp 65000,0;
			}
			close;
		case 2:
			mes "[Ei'felle]";
			mes "You want to become more";
			mes "competent in your job? Um...";
			mes "Alright, we can do that. Just";
			mes "meditate with me, and we'll go";
			mes "do some imagery work together.";
			mes "I'm real good at this you know.";
			next;
			mes "[Ei'felle]";
			mes "J-just... visualize";
			mes "yourself... being...";
			mes "t-totally... awesome!";
			mes "You've gotta see it, and";
			mes "you've gotta feel it in your";
			mes "freakin' heart and mind!";
			emotion ET_HUK, playerattached();
			emotion ET_HUK;
			next;
			mes "[Ei'felle]";
			mes "Yeap, it doesn't matter";
			mes "if it's real or imagined...";
			mes "Your mind will work on";
			mes "whatever you feed it.";
			mes "Placebos and psychosomatic symptoms-- it all ties together.";
			delitem 7515,1; //Marvelous_Medal
			if (checkre(3)) {
				if (Class == Job_Novice) {
					getexp 0,15;
				} else if (Class == Job_Novice_High) {
					getexp 0,25;
				} else if ((Class >= Job_Swordman && Class <= Job_Thief) || Class == Job_Taekwon) {
					if (JobLevel < 11) getexp 0,25;
					else if ((JobLevel > 10) && (JobLevel < 21)) getexp 0,200;
					else if ((JobLevel > 20) && (JobLevel < 31)) getexp 0,300;
					else if ((JobLevel > 30) && (JobLevel < 41)) getexp 0,950;
					else getexp 0,1800;
				} else if ((Class >= Job_Knight && Class <= Job_Crusader2) || (Class >= Job_Swordman_High && Class <= Job_Thief_High)) {
					if (JobLevel < 11) getexp 0,40;
					else if ((JobLevel > 10) && (JobLevel < 21)) getexp 0,300;
					else if ((JobLevel > 20) && (JobLevel < 31)) getexp 0,550;
					else if ((JobLevel > 30) && (JobLevel < 41)) getexp 0,1080;
					else getexp 0,2150;
				} else {
					if (JobLevel < 11) getexp 0,55;
					else if ((JobLevel > 10) && (JobLevel < 21)) getexp 0,330;
					else if ((JobLevel > 20) && (JobLevel < 31)) getexp 0,600;
					else if ((JobLevel > 30) && (JobLevel < 41)) getexp 0,1200;
					else if ((JobLevel > 40) && (JobLevel < 51)) getexp 0,1750;
					else getexp 0,2950;
				}

			/* iRO JEXP Table:
				if (BaseLevel < 21) getexp 0,150;
				else if ((BaseLevel > 20) && (BaseLevel < 31)) getexp 0,200;
				else if ((BaseLevel > 30) && (BaseLevel < 41)) getexp 0,750;
				else if ((BaseLevel > 40) && (BaseLevel < 51)) getexp 0,1800;
				else if ((BaseLevel > 50) && (BaseLevel < 61)) getexp 0,2500;
				else if ((BaseLevel > 60) && (BaseLevel < 71)) getexp 0,3525;
				else if ((BaseLevel > 70) && (BaseLevel < 81)) getexp 0,4125;
				else if ((BaseLevel > 80) && (BaseLevel < 96)) getexp 0,5250;
				else if ((BaseLevel > 95) && (BaseLevel < 111)) getexp 0,6600;
				else if ((BaseLevel > 110) && (BaseLevel < 126)) getexp 0,8175;
				else if ((BaseLevel > 125) && (BaseLevel < 141)) getexp 0,9975;
				else getexp 0,12000;
			*/

			} else {
				if (Class == Job_Novice) {
					getexp 0,30;
				} else if ((Class >= Job_Swordman && Class <= Job_Thief) || Class == Job_Taekwon) {
					if (JobLevel < 11) getexp 0,50;
					else if ((JobLevel > 10) && (JobLevel < 21)) getexp 0,250;
					else if ((JobLevel > 20) && (JobLevel < 31)) getexp 0,1500;
					else if ((JobLevel > 30) && (JobLevel < 41)) getexp 0,7000;
					else getexp 0,20000;
				} else if (Class >= Job_Knight && Class <= Job_Crusader2) {
					if (JobLevel < 11) getexp 0,80;
					else if ((JobLevel > 10) && (JobLevel < 21)) getexp 0,2000;
					else if ((JobLevel > 20) && (JobLevel < 31)) getexp 0,10000;
					else if ((JobLevel > 30) && (JobLevel < 41)) getexp 0,25000;
					else getexp 0,38000;
				} else if (Class == Job_Novice_High) {
					getexp 0,40;
				} else if (Class >= Job_Swordman_High && Class <= Job_Thief_High) {
					if (JobLevel < 11) getexp 0,65;
					else if ((JobLevel > 10) && (JobLevel < 21)) getexp 0,300;
					else if ((JobLevel > 20) && (JobLevel < 31)) getexp 0,2500;
					else if ((JobLevel > 30) && (JobLevel < 41)) getexp 0,10000;
					else getexp 0,25000;
				} else if (Class >= Job_Lord_Knight && Class <= Job_Paladin_2) {
					if (JobLevel < 11) getexp 0,150;
					else if ((JobLevel > 10) && (JobLevel < 21)) getexp 0,2200;
					else if ((JobLevel > 20) && (JobLevel < 31)) getexp 0,13000;
					else if ((JobLevel > 30) && (JobLevel < 41)) getexp 0,27000;
					else if ((JobLevel > 40) && (JobLevel < 51)) getexp 0,38000;
					else getexp 0,40000;
				} else {
					if (JobLevel < 11) getexp 0,50;
					else if ((JobLevel > 10) && (JobLevel < 21)) getexp 0,250;
					else if ((JobLevel > 20) && (JobLevel < 31)) getexp 0,1500;
					else if ((JobLevel > 30) && (JobLevel < 41)) getexp 0,7000;
					else if ((JobLevel > 40) && (JobLevel < 51)) getexp 0,20000;
					else if ((JobLevel > 50) && (JobLevel < 61)) getexp 0,30000;
					else getexp 0,38000;
				}
			}
			close;
		case 3:
			mes "[Ei'felle]";
			mes "What th--?! You sure";
			mes "you don't want anything?";
			mes "Hm, well, I think it's kind";
			mes "of bad karma if I don't give";
			mes "you anything in return, so...";
			mes "Think of something. Quick.";
			next;
			mes "["+ strcharinfo(0) +"]";
			mes "Er, just use the medals";
			mes "to further your manufacturing";
			mes "research, and then you can";
			mes "pay me back if your develop";
			mes "something new. It's, um, like";
			mes "an investment in your work!";
			next;
			mes "[Ei'felle]";
			mes "Yeah... Yeah.";
			mes "Okay, I see where";
			mes "you're coming from.";
			mes "That's pretty smart.";
			mes "Okay, I'll repay you when";
			mes "we develop something new!";
			next;
			mes "[Ei'felle]";
			mes "Anyway, I need as many";
			mes "medals as I can get as";
			mes "soon as I can. How many";
			mes "medals do you think you";
			mes "can give me right now?";
			next;
			switch(select("Take them all.:How about this much?:No, I changed my mind.")) {
			case 1:
				mes "[Ei'felle]";
				mes "Th-thank you!";
				mes "Thank you so much!";
				mes "Your help will greatly";
				mes "advance my research,";
				mes "and I promise to repay";
				mes "you as soon as I can!";
				.@medals = countitem("Marvelous_Medal");
				delitem 7515, .@medals; //Marvelous_Medal
				ein_medal01 += .@medals;
				close;
			case 2:
				mes "[Ei'felle]";
				mes "Alright, please enter the";
				mes "number of medals that";
				mes "you're willing to give me.";
				mes "Please don't enter any";
				mes "number greater than 100.";
				next;
				while(1) {
					input .@input;
					if (!.@input) {
						mes "[Ei'felle]";
						mes "Aw, so you've decided";
						mes "to cancel? Well, it's your";
						mes "choice, but I'm still so";
						mes "disappointed. Please come";
						mes "back if you change your mind...";
						close;
					} else if (.@input < 1 || .@input > 100) {
						mes "[Ei'felle]";
						mes "Remember, you can only";
						mes "enter a number from 1 to 100.";
						mes "If you want to give me more";
						mes "medals, then perhaps you";
						mes "should just give them all to me~";
						next;
					} else break;
				}
				if (countitem("Marvelous_Medal") < .@input) {
					mes "[Ei'felle]";
					mes "I'm sorry, but I don't";
					mes "think you have that many";
					mes "medals with you. Make sure";
					mes "that you offer me an amount of";
					mes "medals that you actually have.";
					emotion ET_SMILE;
					close;
				} else {
					mes "[Ei'felle]";
					mes "Th-thank you!";
					mes "Thank you so much!";
					mes "Your help will greatly";
					mes "advance my research,";
					mes "and I promise to repay";
					mes "you as soon as I can!";
					delitem 7515, .@input; //Marvelous_Medal
					ein_medal01 += .@input;
					close;
				}
			case 3:
				mes "[Ei'felle]";
				mes "Oh, alright...";
				mes "Still, I really need";
				mes "those medals, so if you";
				mes "change your mind, please";
				mes "come back as soon as you can.";
				close;
			}
		case 4:
			mes "[Ei'felle]";
			mes "Well, I've been able to";
			mes "create a Glittering Jacket";
			mes "using the medals that you've";
			mes "brought to me. But I just know";
			mes "I can make something better";
			mes "if you'd bring me more!";
			next;
			mes "[Ei'felle]";
			mes "As of now, I have the metal";
			mes "from ^FF0000"+ ein_medal01 +"^000000 Prizes Medals that";
			mes "you've donated to me. The more";
			mes "that you bring, the closer I can get to making a new breakthrough!";
			close;
	}

S_BonusReward:
	mes "[Ei'felle]";
	.@arg1 = getarg(1);
	if (.@arg1 == 2319) {
		mes "Do you really want";
		mes "this "+getitemname(.@arg1)+"?";
		mes "You may want to forego this";
		mes "reward in favor of getting";
		mes "something better later...";
	} else if (.@arg1 == 2345 || .@arg1 == 2347 || .@arg1 == 2349 || .@arg1 == 2351) {
		mes "Are you sure that you want";
		mes "to accept this set of slotted";
		if (.@arg1 == 2345) mes "Fire property Armor? If you do,";
		else if (.@arg1 == 2351) mes "Earth property Armor? If you do,";
		else if (.@arg1 == 2349) mes "Wind property Armor? If you do,";
		else if (.@arg1 == 2347) mes "Water property Armor? If you do,";
		mes "I'll need more medals from you";
		mes "to make further advancements";
		mes "in my manufacturing research.";
	} else {
		mes "So you wish to have a";
		mes getitemname(.@arg1)+"? If you choose";
		mes "to have this Level 4 Weapon,";
		mes "I'll need to melt many of the";
		mes "medals that you've donated";
		mes "to me. Shall we proceed?";
	}
	next;
	if (select("Decline:Accept") == 1) {
		mes "["+ strcharinfo(0) +"]";
		mes "W-wait, I changed my";
		mes "mind. Would it be fine";
		mes "if I asked you to give";
		mes "me a reward later?";
		next;
		mes "[Ei'felle]";
		if (!getiteminfo(.@arg1,13)) { //use item level to determine if the item is armor (no weapon level)
			mes "Of course, of course.";
			mes "Remember, if you donate";
			mes "more medals to me, then";
			mes "I'll be able to create items";
			mes "of higher quality for you~";
		} else {
			mes "I don't think I can develop";
			mes "anything better than these Level 4 Weapons, but after making";
			mes "such a big investment, you should decide on what you want carefully.";
		}
		close;
	}
	mes "[Ei'felle]";
	if (.@arg1 == 2319) {
		mes "Here you are, I trust that";
		mes "this "+getitemname(.@arg1)+" will";
		mes "serve you well. Thank you";
		mes "for your help, and I hope that";
		mes "you'll continue to donate your";
		mes "medals for my metal research~";
	} else if (.@arg1 == 2345 || .@arg1 == 2347 || .@arg1 == 2349 || .@arg1 == 2351) {
		mes "Great choice! I'm sure";
		if (.@arg1 == 2345) mes "that this set of slotted Fire";
		else if (.@arg1 == 2351) mes "that this set of slotted Earth";
		else if (.@arg1 == 2349) mes "that this set of slotted Wind";
		else if (.@arg1 == 2347) mes "that this set of slotted Water";
		mes "property Armor will serve you";
		mes "well. Thank you for your help,";
		mes "and if you get more medals,";
		mes "please donate them to me~";
	} else {
		mes "Once again, I'd like to";
		mes "thank you for providing";
		mes "me with all of those medals.";
		mes "I imagine it must have been";
		mes "difficult. In any case, I would";
		mes "appreciate your continued help~";
	}
	ein_medal01 -= getarg(0);
	getitem .@arg1,1;
	close;
}